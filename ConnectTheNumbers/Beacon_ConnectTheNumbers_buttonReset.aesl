# BEACON - the targets for the Connect The Numbers game. Should be set out according to a pattern, e.g. a star or a heart

# variables
var id = 1		# the value that the beacon represents
var counter = 0	# counting how long to display the response to the walker when encountered
var sound = 1
var right = 0

# setting up
call leds.top(0,32,32)

timer.period[0] = 2000


call prox.comm.enable(1)
prox.comm.tx = id

onevent buttons
	if button.center == 1 and counter == -1 then
		counter = 0
		call prox.comm.enable(1)
		prox.comm.tx = id
		emit reset
	end

# regular event. handles the waiting time after getting a message from the walker.
onevent prox
	if counter == 0 then
		call leds.top(0,32,32)
	else
		if counter >= 50 then
			call leds.top(0,0,0)
			call sound.system(1)
			call prox.comm.enable(0)
			counter = -1
		elseif counter != -1 then
			counter++
		end
	end

# if the beacon gets a signal when the robot is close enough, it checks if the robot confirms by sending back the beacons ID. Sets of the counter and displays helpful behaviour	
onevent prox.comm
	if prox.horizontal[0] > 3000 or prox.horizontal[1] > 3000 or prox.horizontal[2] > 3000 or prox.horizontal[3] > 3000 or prox.horizontal[4] > 3000 then
		if counter == 0 then
			counter = 1
		end
		if prox.comm.rx == id and counter <=50 then
			call leds.top(0,32,0)
			right = 1
			if sound == 1  then
				call sound.system(3)
			end
			sound = (sound + 1) % 8
		elseif counter <= 50 then
			right = 0
			call leds.top(32,0,0)
			if sound == 1  then
				call sound.system(5)
			end
			sound = (sound + 1) % 8
		end
	end
	
onevent reset
	counter = 0
	call prox.comm.enable(1)
	prox.comm.tx = id

	